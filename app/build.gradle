plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
}
def json_huawei_release = "src/huawei/agconnect-services.json"
def json_google = "src/google/google-services.json"

if (getGradle().getStartParameter().getTaskRequests().toString().contains('Huawei')) {
    if (project.file(json_huawei_release).exists()) {
        apply plugin: "com.huawei.agconnect"
    }else {
        println "missing: ${project.file(json_huawei_release)}"
    }
}

if (getGradle().getStartParameter().getTaskRequests().toString().contains('Google')) {
    if (project.file(json_google).exists()) {
        println "found: ${project.file(json_google)}"
        apply plugin: "com.google.gms.google-services"
        apply plugin: "com.google.firebase.crashlytics"
    } else {
        println "missing: ${project.file(json_google)}"
    }
}

android {
    namespace 'com.dauto.moongarden'
    compileSdk 33

    defaultConfig {
        applicationId "com.dauto.moongarden"
        minSdk 23
        targetSdk 33
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }
    sourceSets {
        google.java.srcDirs += 'src/google/java'
        huawei.java.srcDirs += 'src/huawei/java'
    }

    flavorDimensions "mobileServices"
    productFlavors {

        google {
            dimension "mobileServices"
            applicationIdSuffix ".appMarket"
        }

        huawei {
            dimension "mobileServices"
            applicationIdSuffix ".huawei"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding = true
    }
}

dependencies {

    implementation project(path: ':domain')
    implementation project(path: ':data')

    googleImplementation platform('com.google.firebase:firebase-bom:32.0.0')
    googleImplementation 'com.google.firebase:firebase-analytics-ktx'
    googleImplementation 'com.google.firebase:firebase-crashlytics-ktx:18.3.7'
    googleImplementation "com.google.android.gms:play-services-location:21.0.1"

    huaweiImplementation 'com.huawei.agconnect:agconnect-core:1.9.0.300'
    huaweiImplementation 'com.huawei.hms:hianalytics:6.10.0.300'
    huaweiImplementation 'com.huawei.agconnect:agconnect-crash:1.9.0.300'
    huaweiImplementation 'com.huawei.agconnect:agconnect-apms:1.6.1.301'

    implementation 'androidx.navigation:navigation-ui-ktx:2.5.3'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.3'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.2.0-alpha01"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.6.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.6.1"
    implementation 'androidx.fragment:fragment-ktx:1.5.7'
    implementation("androidx.viewpager2:viewpager2:1.0.0")
    implementation 'com.github.bumptech.glide:glide:4.15.1'
    kapt 'com.github.bumptech.glide:compiler:4.15.0'

    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}